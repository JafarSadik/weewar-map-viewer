apply plugin: 'java'
apply plugin: 'org.springframework.boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

springBoot {
    mainClass = 'com.github.weewar.mapviewer.WeewarMapViewer'
}

buildscript {
    repositories { mavenCentral() }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE'
    }
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web:1.5.6.RELEASE', {
        exclude module: 'spring-boot-starter-tomcat'
    }
    compile 'org.springframework.boot:spring-boot-starter-jetty:1.5.6.RELEASE'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10+'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}


task convertMaps {
    description = "Converts original weewar maps from XML to JSON format"
    group = "weewar map viewer"

    def targetMapsDir = "${project.rootDir}/src/main/resources/public/maps"
    def script = "${project.rootDir}/bin/maps/convert-maps.groovy"
    def args = ["--force", targetMapsDir] as String[]
    def bindings = [forceOverride: true, targetDir: targetMapsDir]

    doLast {
        def shell = new GroovyShell(new Binding(bindings))
        shell.run(new File(script), args)
    }
}
